PREFIX pj: <https://openprovenance.org/ns/parajudica#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

# Evaluate simple implications with single fromLabel
# Uses label-scope index for efficient lookup

CONSTRUCT {
    ?newAssertion a pj:ComplianceAssertion ;
                  pj:assertedOn ?container ;
                  pj:assertedInScope ?scope ;
                  pj:assertsLabel ?toLabel ;
                  pj:assertedByFramework ?framework ;
                  pj:derivedFrom ?assertion ;
                  pj:derivedVia pj:ImplicationRule .
}
WHERE {
    # Find implications first
    ?framework pj:declaresImplication ?impl .
    ?impl a pj:ConditionalImplication ;
          pj:fromLabel ?fromLabel ;
          pj:toLabel ?toLabel .

    # Find containers with fromLabel in specific scopes (using index)
    ?container pj:hasLabelInScope ?labelScope .
    ?labelScope pj:label ?fromLabel ;
               pj:scope ?scope .

    # Find the assertion that created this label (for derivedFrom)
    ?assertion a pj:ComplianceAssertion ;
               pj:assertedOn ?container ;
               pj:assertedInScope ?scope ;
               pj:assertsLabel ?fromLabel .

    # Check for conditions
    OPTIONAL { ?impl pj:hasCondition ?condition }

    # Check for evaluations separately (workaround for Oxigraph OPTIONAL issue)
    OPTIONAL {
        SELECT ?impl ?container ?scope ?eval
        WHERE {
            ?impl pj:hasCondition ?condition2 .
            ?eval a pj:ConditionEvaluation ;
                  pj:evaluatesCondition ?condition2 ;
                  pj:evaluatedOn ?container ;
                  pj:evaluatedInScope ?scope ;
                  pj:evaluationResult "true"^^xsd:boolean .
        }
    }

    # Only proceed if no condition or condition is evaluated as true
    FILTER(!BOUND(?condition) || BOUND(?eval))

    # Create deterministic assertion ID
    BIND(IRI(CONCAT("urn:skolem:",
                    ENCODE_FOR_URI(STR(?container)), "_",
                    ENCODE_FOR_URI(STR(?toLabel)), "_",
                    ENCODE_FOR_URI(STR(?framework)), "_",
                    ENCODE_FOR_URI(STR(?scope)), "_assertion")) AS ?newAssertion)

    # Check target doesn't exist using the deterministic ID
    FILTER NOT EXISTS {
        ?newAssertion a pj:ComplianceAssertion .
    }
}
